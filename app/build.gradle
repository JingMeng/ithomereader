apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: 'config.gradle'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def signingConfigFile = rootProject.file("signing.properties")
def signingProperties = new Properties()
signingProperties.load(new FileInputStream(signingConfigFile))

android {
    signingConfigs {
        release {
            keyAlias signingProperties['keyAlias']
            keyPassword signingProperties['keyPassword']
            storeFile file(signingProperties['storeFile'])
            storePassword signingProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "me.ikirby.ithomereader"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 137
        versionName "4.6.1"
        resConfigs "zh-rCN"
        versionNameSuffix '-' + getGitHash()
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'update'
    productFlavors {
        normal {
            dimension 'update'
            manifestPlaceholders = [updateUrl: updateUrl]
        }
        noupdate {
            dimension 'update'
            versionNameSuffix '-pub'
            manifestPlaceholders = [updateUrl: '']
        }
    }
    packagingOptions {
        exclude 'META-INF/CHANGES'
        exclude 'META-INF/README.md'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/*.version'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha01'
    implementation 'androidx.fragment:fragment:1.1.0-alpha04'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha02'
    implementation 'androidx.preference:preference:1.1.0-alpha03'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.github.chrisbanes:PhotoView:2.2.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'
    //    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    //    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

repositories {
    mavenCentral()
}