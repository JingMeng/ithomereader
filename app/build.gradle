apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: 'config.gradle'

def androidx_version = '1.0.0'
def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "me.ikirby.ithomereader"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 125
        versionName "4.3.17"
        resConfigs "zh-rCN"
        versionNameSuffix '-' + getGitHash()
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            useProguard false
        }
    }
    flavorDimensions 'update'
    productFlavors {
        normal {
            dimension 'update'
            manifestPlaceholders = [updateUrl: updateUrl]
        }
        noupdate {
            dimension 'update'
            versionNameSuffix '-pub'
            manifestPlaceholders = [updateUrl: '']
        }
    }
    packagingOptions {
        exclude 'META-INF/CHANGES'
        exclude 'META-INF/README.md'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/*.version'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.26.1'
    implementation "androidx.viewpager:viewpager:$androidx_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidx_version"
    implementation "androidx.fragment:fragment:$androidx_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_version"
    implementation "androidx.preference:preference:$androidx_version"
    implementation "androidx.appcompat:appcompat:$androidx_version"
    implementation "androidx.palette:palette:$androidx_version"
    implementation "androidx.browser:browser:$androidx_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
//    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

repositories {
    mavenCentral()
}