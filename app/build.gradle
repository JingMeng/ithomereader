apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: 'config.gradle'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def signingConfigFile = rootProject.file("signing.properties")
def signingProperties = new Properties()
signingProperties.load(new FileInputStream(signingConfigFile))

android {
    signingConfigs {
        release {
            keyAlias signingProperties['keyAlias']
            keyPassword signingProperties['keyPassword']
            storeFile file(signingProperties['storeFile'])
            storePassword signingProperties['storePassword']
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    defaultConfig {
        applicationId "me.ikirby.ithomereader"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 176
        versionName "5.0.0-beta06"
        resConfigs "zh-rCN"
        versionNameSuffix '-' + getGitHash()
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    flavorDimensions 'update'
    productFlavors {
        normal {
            dimension 'update'
            manifestPlaceholders = [updateUrl: updateUrl]
        }
        noupdate {
            dimension 'update'
            versionNameSuffix '-pub'
            manifestPlaceholders = [updateUrl: '']
        }
    }
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        exclude 'META-INF/CHANGES'
        exclude 'META-INF/README.md'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/*.version'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    implementation 'androidx.core:core-ktx:1.5.0-alpha05'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-beta02'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-beta01'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.3.0-beta01'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    //    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    //    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

repositories {
    mavenCentral()
}